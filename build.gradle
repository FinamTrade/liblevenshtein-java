plugins {
  id 'java'
  id 'maven'
  id "me.champeau.gradle.jmh" version "0.4.3"
  id 'com.bmuschko.nexus' version '2.3.1'
}

repositories {
  mavenCentral()
}

apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'idea'

jmh {
  humanOutputFile = null
  warmupIterations = 10
  iterations = 10
  fork = 1
}

group = 'ru.finam'
version = '3.0.0' // http://semver.org/

description = 'A library for spelling-correction based on Levenshtein Automata.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  testCompile 'org.assertj:assertj-core:3.8.0'
  testCompile 'org.mockito:mockito-all:1.10.19'
  testCompile 'org.testng:testng:6.11'
}

test {
  useTestNG() {
    // Run as many tests in-parallel as there are CPU cores
    parallel 'methods'
    threadCount Runtime.runtime.availableProcessors()
  }

  testLogging {
    //events 'passed', 'skipped', 'failed'
    events 'skipped', 'failed'
    exceptionFormat 'full'
    showStandardStreams true
    showExceptions true
    showStackTraces true
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.4'
}

modifyPom {
  project {
    name 'liblevenshtein-lite'
    description 'A library for spelling-correction based on Levenshtein Automata.'
    url 'https://github.com/FinamTrade/liblevenshtein-java/'
    scm {
      connection 'scm:git:git@github.com:FinamTrade/liblevenshtein-java.git'
      developerConnection 'scm:git:git@github.com:FinamTrade/liblevenshtein-java.git'
      url 'git@github.com:FinamTrade/liblevenshtein-java.git'
    }

    licenses {
      license {
        name 'MIT License'
        url 'https://raw.githubusercontent.com/FinamTrade/liblevenshtein-java/master/LICENSE'
      }
    }
    developers {
      developer {
        id 'ruslansennov'
        name 'Ruslan Sennov'
        email 'ruslan.sennov@gmail.com'
      }
      developer {
        id 'dylon'
        name 'Dylon Edwards'
        email 'dylon.devo+liblevenshtein-java@gmail.com'
      }
    }
  }
}

